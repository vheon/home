# vheon prompt theme

prompt_title_preexec_hook() {
  # shows the current dir and executed command in the title when a process is active
  print -Pn "\e]0;"
  echo -nE "$PWD:t: $2"
  print -Pn "\a"
}

prompt_vheon_precmd_hook() {
  vcs_info
}

+vi-git-untracked() {
  if [[ $(git rev-parse --is-inside-work-tree 2> /dev/null) == 'true' ]] && \
      git status --porcelain | fgrep '??' &> /dev/null ; then
    # untracked symbol
    hook_com[staged]+='+'
  fi
}


# +vi-git-untracked() {
#   if [[ -n $(git ls-files --other --exclude-standard --directory) ]]; then
#     # untracked symbol
#     hook_com[staged]+='+'
#   fi
# }

prompt_suspended_count() {
  if [[ -n $(jobs -s) ]]; then
    print '☰ '
  fi
}

prompt_vheon_setup() {
  autoload -Uz vcs_info
  autoload -Uz add-zsh-hook

  add-zsh-hook preexec prompt_title_preexec_hook
  add-zsh-hook precmd prompt_vheon_precmd_hook

  zstyle ':vcs_info:*' enable git hg
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' unstagedstr '?'
  zstyle ':vcs_info:*' stagedstr '!'

  zstyle ':vcs_info:git:*' formats ' (±)-[%F{yellow}%b%f%u%c]'
  zstyle ':vcs_info:git:*' actionformats ' %F{yellow}%b%f|%F{magenta}%a%f%u%c'
  zstyle ':vcs_info:git*+set-message:*' hooks git-untracked

  zstyle ':vcs_info:hg:*' formats ' (☿)-[%F{yellow}%b%f%u%c]'

  zstyle ':vcs_info:git-svn:*' formats ' (±/☇)-[%F{yellow}%b%f%u%c]'
  zstyle ':vcs_info:git-svn:*' actionformats ' %F{yellow}%b%f|%F{magenta}%a%f%u%c'
  zstyle ':vcs_info:git-svn*+set-message:*' hooks git-untracked

  PROMPT=''

  # show username@host if logged in through SSH
  [[ "$SSH_CONNECTION" != '' ]] && PROMPT+='%F{green}%n%f@%F{magenta}%m%f '

  # PROMPT+='%F{yellow}$(prompt_suspended_count)%f'
  PROMPT+='%F{blue}%~%f'
  PROMPT+='${vcs_info_msg_0_}'
  PROMPT+='$prompt_newline'
  PROMPT+='%F{yellow}$(prompt_suspended_count)%f'
  PROMPT+='%(?.%F{green}.%F{red})❯%f '
}

prompt_vheon_setup "$@"

# vim:set ft=zsh:
